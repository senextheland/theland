#class {regen} {open}

#var {regen_default_mana_threshold} {450}
#var {regen_default_move_threshold} {150}

#function {regen_mana_diff} {
  #math {result} {$max_mana - %1}
}

#function {regen_move_diff} {
  #math {result} {$max_refresh - %1}
}

#function {regen_robot_needs_regen} {
  debug REGEN robot-needs-regen? $group_stats[Silk][move];
  #math {result} {$group_stats[Silk][move] < 1100}
}

#function {regen_needed} {
  #math {result} {
    $mana < @regen_mana_diff{$regen_default_mana_threshold}
    || $movement < @regen_move_diff{$regen_default_move_threshold}
    || @regen_robot_needs_regen{} == 1
  }
}

#function {is_regen_done} {
  debug IS-REGEN-DONE? m:$mana --> @regen_mana_diff{%1} v:$movement --> @regen_move_diff{%2} r:@regen_robot_needs_regen{};
  #math {result} {
    $mana >= @regen_mana_diff{%1}
    && $movement >= @regen_move_diff{%2}
    && @regen_robot_needs_regen{} != 1
  }
}

#alias {cmd_regen_some} {
  cmd_regen {$regen_default_mana_threshold} {$regen_default_move_threshold} {%1} {%2}
}

#alias {cmd_regen_fairy} {
  cmd_regen_fairy_until {150} {150} {%1} {%2}
}

#alias {cmd_regen} {
  #class {cmd_regen} {open};

  #var {regen_mana_threshold} {%1};
  #var {regen_move_threshold} {%2};
  #alias {on_regen_done} {regen_off; %3};
  #alias {on_regen_fail} {regen_off; %4};
  #var {regen_state} {off};

  #alias {regen_mana_hook} {
    debug REGEN MANA UP;
    #if {@is_regen_done{$regen_mana_threshold;$regen_move_threshold} == 1} {
      debounce_regen_done
    }  
  };

  #alias {regen_move_hook} {
    debug REGEN MOVE UP;
    #if {@is_regen_done{$regen_mana_threshold;$regen_move_threshold} == 1} {
      debounce_regen_done
    }  
  };

  #alias {regen_tick_hook} {
    debug REGEN TICK HOOK: $group_stats;
    #if {@regen_robot_needs_regen{} == 1} {
      cmd_group_stats {regen_robot_hook}
    }
  };

  #alias {regen_robot_hook} {
    #if {@is_regen_done{$regen_mana_threshold;$regen_move_threshold} == 1} {
      debounce_regen_done
    }  
  };

  #alias {debounce_regen_done} {
    #undelay {debounce_regen_done};
    #delay {debounce_regen_done} {debounced_regen_done} {0.2}
  };

  #alias {debounced_regen_done} {
    tell group 'REGEN --> done;
    on_regen_done  
  };

  #alias {regen_off} {
    hook {on_mana_up} {remove} {cmd_regen};
    hook {on_movement_up} {remove} {cmd_regen};
    hook {on_tick} {remove} {cmd_regen};
    #if {"$regen_state" == "on"} {unregen};
    #class {cmd_regen} {kill}
  };

  hook {on_mana_up} {add} {cmd_regen} {trampoline {regen_on_mana_up} {regen_mana_hook}};
  hook {on_movement_up} {add} {cmd_regen} {trampoline {regen_on_movement_up} {regen_move_hook}};
  hook {on_tick} {add} {cmd_regen} {regen_tick_hook};

  #alias {regen_with_score} {
    debug REGEN CHECK mana=$mana max=$score_max_mana;
    debug REGEN GROUP? $group_stats;
    #if {@is_regen_done{$regen_mana_threshold;$regen_move_threshold} == 1} {
      debug REGEN --> skip;
      on_regen_done
    } {
      tell group 'REGEN --> start (m:$mana to @regen_mana_diff{$regen_mana_threshold} v:$movement to @regen_move_diff{$regen_move_threshold});
      #var {regen_state} {on};
      regen
    }  
  };

  #class {cmd_regen} {save};
  #class {cmd_regen} {close};

  cmd_group_score {regen_with_score}
}

#alias {cmd_regen_fairy_until} {
  #class {cmd_regen_fairy} {open};

  #var {regen_mana_threshold} {%1};
  #var {regen_move_threshold} {%2};
  #alias {regen_fairy_done} {regen_fairy_end; %3};
  #alias {regen_fairy_fail} {regen_fairy_end; %4};

  #alias {regen_fairy_end} {
    hook {on_mana_up} {remove} {regen_fairy_mana_up};
    #class {cmd_regen_fairy} {kill}
  };

  #alias {regen_fairy_mana_up} {
    debug REGEN-FAIRY MANA UP: mana=$mana max=$score_max_mana threshold=$regen_mana_threshold;
    #if {$regen_mana_threshold == -1 || $mana > ($score_max_mana - $regen_mana_threshold)} {
      debug REGEN-FAIRY-DONE;
      regen_fairy_done
    } {
      debug REGEN-FAIRY-MAYBE-BUY;
      trampoline {cmd_regen_fairy_until} {maybe_buy_mana}
    }
  };

  #alias {maybe_buy_mana} {
    cmd_score {
      #if {$score_gold_coins >= 300000} {
        debug REGEN-FAIRY: has 300k;
	buy 10
      } {
        debug REGEN-FAIRY: not enough coins;
	on_regen_fairy_done
      }      
    }
  };

  hook {on_mana_up} {add} {regen_fairy_mana_up} {regen_fairy_mana_up};
  
  #class {cmd_regen_fairy} {save};
  #class {cmd_regen_fairy} {close};

  cmd_score {
    #if {$movement < ($score_max_movement - $regen_move_threshold)} {
      debug REGEN-FAIRY: buy move;
      buy 9
    } {
      debug REGEN-FAIRY: skip move
    };
    trampoline {cmd_regen_fairy_until} {regen_fairy_mana_up}
  }
}

#class {regen} {save}
#class {regen} {close}