#class {fish} {open}

#alias {fish_container} {golden-case}

#alias {fish_once_end} {#nop}
#alias {fish_once} {
  #var {fish_state} {idle};
  
  #class {fish_once} {open};
  
  #var {fish_target} {%1};
  #alias {fish_once_done} {fish_once_end; %2};
  #alias {fish_once_fail} {fish_once_end; %3};

  #alias {fish_once_end} {
    debug FISH-ONCE-END!!!;o
    hook {on_fight_start} {remove} {fish};
    put all.frozen-fish $fish_container;        
    #class {fish_once} {kill};
    #alias {fish_once_end} {#nop};
    #var {fish_state} {idle}
  };

  hook {on_fight_start} {once} {fish} {fish_once_end};

  #alias {get_fish_and_throw} {
    exec {get_fish_and_throw} {cmd_get_fish_out} {throw_fish} {no_fish}
  };

  #alias {throw_fish} {
    #if {"$combat_status" == "idle"} {
      #var {fish_state} {throw};
      throw frozen-fish $fish_target
    } {
      debug THROW FISH --> fighting queue=@has_labeled_task{global_tasks[items];get_fish_and_throw};
      #var {fish_state} {idle};
      trampoline {throw_fish} {fish_once_fail {fighting}}
    }
  };

  #alias {no_fish} {
    debug NO FISH!! queue=@has_labeled_task{global_tasks[items];get_fish_and_throw};
    fish_once_fail {no-dagger}
  };

  #nop There is a race condition between throwing a fish from inventory and the recovery logic ;
  #nop that blesses a fish and returns it to the container. So this action may trigger because ;
  #nop a fish ready to throw was put away before it can be thrown.                             ;
  #action {You don't have that!} {
    trampoline {need_fish_to_throw} {get_fish_and_throw}
  };
  
  #action {You don't see anybody around meeting that description.} {fish_once_fail {no-mob}} {4}; 

  #alias {cmd_get_fish_out} {
    exec_task {cmd_get_fish_out}
      {debug GET-FISH: START; get frozen-fish $fish_container}
      {%%1}
      {debug GET-FISH FAIL (%%%); %%2 %%%0}
      {      
        #action {You get a frozen fish from} {debug GET-FISH: OK; task_cmd_get_fish_out_done};
        #action {You do not see a fish in} {debug GET-FISH: NONE; task_cmd_get_fish_out_fail};
        #alias {task_cmd_get_fish_out_end_hook} {debug GET-FISH: END}
    }
  };

  #action {You catch a frozen fish} {
    #var {fish_state} {idle};
    throw_fish
  };
  
  #action {A frozen fish falls to the ground near you, since you couldn't catch it.} {
    #var {fish_state} {idle};
    exec {cmd_retrieve_fish} {cmd_retrieve_fish} {throw_fish} {fish_lost}
  };

  #alias {fish_lost} {
    debug FISH LOST!!  
  };

  #alias {cmd_retrieve_fish} {
    exec_task {cmd_retrieve_fish} {get all.frozen-fish} {%%1} {%%2} {
      #action {You get a frozen fish.} {task_cmd_retrieve_fish_done}
    }
  };

  #nop There are often a few related triggers for losing a fish. Temporarily set     ;
  #nop the `fish_state` to `lost` to prevent multiple triggers for the same logical  ;
  #nop event. Throwing a new fish transitions the state back to `throw` then `idle`. ;
  
  #alias {debounce_fish_lost} {
    #if {"$fish_state" != "lost"} {
      #var {fish_state} {lost};
      on_fish_lost {%%1} {get_fish_and_throw} {get_fish_and_throw}
    } {
      debug DEBOUNCE-FISH-LOST -- ignore
    }
  };

  #action {A frozen fish suddenly reverses direction and flies %%1!} {
    debounce_fish_lost {%%1}
  };
  
  #action {caught a frozen fish right out of the air, and its coming straight for you!} {
    debounce_fish_lost {}
  };
  
  #action {You see a frozen fish hit a wall up ahead before it can return to your hand.} {
    debounce_fish_lost {}  
  };

  #class {fish_once} {save};
  #class {fish_once} {close};

  throw_fish
}

#alias {fish} {fish_once}

#class {fish} {save}
#class {fish} {close}