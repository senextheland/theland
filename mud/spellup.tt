#class {spellup} {open}

#var need_spells {bless;armor}

#alias {spellup} {
  #class {spellup_once} {open};

  #alias {spellup_done_continuation} {spellup_once_end; %1};
  #list {attr_spells} {create};

  #action {Spell : '{[\w\s]+}'} {#list {attr_spells} add {%%1}};  

  #action {You are susceptible to} {
    hook {on_prompt} {once} {spellup} {
      diff_need_spells
    }
  };

  #alias {cmd_cast_if_needed} {
    #list {attr_spells} {find} {%%1} {result};
    debug SPELLS attr_spells=$attr_spells;
    debug SPELLUP: probe %%1 --> $result;
    #if {$result == 0} {
      cmd_cast_me {%%1} {%%2} {%%3}
    } {
      #var {spell_state[%%1]} {up};
      %%2
    }
  };

  #alias {diff_need_spells} {
    #nop FIXME -- it would be better to execute with foreach_exec_task, but this tasks blocks them from running;
    #local {spell} {};
    #foreach {$need_spells} {spell} {
      cmd_cast_if_needed {$spell} {#nop} {#nop}
    };
    #if {"$spell_state[poison]" == "up"} {
      debug SPELLUP: REMOVE POISON!;
      cmd_cast_me {remove poison} {spell_state_remove {poison}} {#nop}
    };
    trampoline {diff_need_spells} {spellup_done_continuation}
  };

  #alias {spellup_once_end} {
    #class {spellup_once} {kill}
  };

  #class {spellup_once} {save};
  #class {spellup_once} {close};

  attr
}

#alias {need_spell_add} {
  #local {temp_spells} {$need_spells};
  #list {temp_spells} {explode} {;};
  #list {temp_spells} {find} {%1} {result};
  #if {"$result" == "0"} {
    #list {temp_spells} {add} {%1};
    #list {temp_spells} {collapse} {;};
    #var {need_spells} {$temp_spells}
  }
}

#alias {need_spell_remove} {
  #local {temp_spells} {$need_spells};
  #list {temp_spells} {explode} {;};
  #list {temp_spells} {find} {%1} {result};
  #if {"$result" != "0"} {
    #list {temp_spells} {delete} {$result};
    #list {temp_spells} {collapse} {;};
    #var {need_spells} {$temp_spells}
  }  
}

#alias {quick_spellup_continuation} {
  #local {quick_spellup_need_spells} {false};
  #local {spell_name} {};
  
  #foreach {$need_spells} {spell_name} {
    #if {"${spell_state[$spell_name]}" == "down"} {
      debug QUICK-SPELLUP: $spell_name is DOWN;
      #var {quick_spellup_need_spells} {true}
    }
  };

  #if {"$spell_state[poison]" == "up"} {
    debug QUICK-SPELLUP: POISON!;
    #var {quick_spellup_need_spells} {true}
  };

  #if {"$quick_spellup_need_spells" == "true" && "$combat_status" == "idle"} {
    spellup {%1}
  } {
    trampoline {quick_spellup_continuation} {%1}
  }
}

#alias {cmd_quick_spellup} {
  exec_task {cmd_quick_spellup} {#show cmd_quick_spellup} {%1} {%2} {
    #action {cmd_quick_spellup} {quick_spellup_continuation {task_cmd_quick_spellup_done}}
  }
}

#var {mode_spirit_armor} {off}
#alias {spirit_armor_off} {
  #var {mode_spirit_armor} {off}
}

#alias {spirit_armor_on} {  
  #class {spirit_armor} {open};

  #alias {spa_down} {
    #var {mode_spa} {down};
    cast_unless_fighting {spiritual armor}
  };  

  #class {spa_group} {save};
  #class {spa_group} {close};

  #alias {spa_off} {
    #alias {spa_off} {#nop};
    #class {spa_group} {kill};
    need_spell_remove {spiritual armor}
  };

  need_spell_add {spiritual armor};
  spellup
}

hook {sanc_down} {create}
hook {protection_down} {create}
hook {strength_down} {create}
hook {bless_down} {create}
hook {armor_down} {create}
hook {shield_down} {create}
hook {fly_down} {create}
hook {spirit_armor_down} {create}

#var spell_state {}
#alias {spell_state_down} {
  #var {spell_state[%1]} {down};
  hook {%1_down} {fire}
}

#alias {spell_state_up} {
  #var spell_state[%1] {up}
}

#alias {spell_state_remove} {
  #if {"$spell_state[%1]" != ""} {
    #unvar {spell_state[%1]}
  }
}

#action {Your protective magic dissipates.} {spell_state_down {sanc}; reset_clock {tick}}
#action {Evil holds dominion over you again as your warding powers fade.} {
  spell_state_down {protection};
  reset_clock {tick}
}
#action {You don't feel as strong.} {spell_state_down {strength}; reset_clock {tick}}
#action {You feel less righteous.} {spell_state_down {bless}; reset_clock {tick}}
#action {You feel less protected.} {spell_state_down {armor}; reset_clock {tick}}
#action {Your shield of protection flickers, then disappears.} {spell_state_down {shield}; reset_clock {tick}}
#action {You sink towards the ground as your magic wears off.} {spell_state_down {fly}; reset_clock {tick}}
#action {Your holy protection disappears.} {spell_state_down {spiritual armor}}

#action {You call on your diety to protect you, and are engulfed in shaft of light.} {spell_state_up {spiritual armor}}
#action {You feel the blessing of a diety upon you.} {spell_state_up {bless}}
#action {You feel someone protecting you.} {spell_state_up {armor}}
#action {You feel very sick.} {spell_state_up {poison}}
#action {You feel better.} {spell_state_remove {poison}}
#action {A warm feeling runs through your body.} {spell_state_remove {poison}}

#class {spellup} {save}
#class {spellup} {close}