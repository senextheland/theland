#nop

hook {on_rip} {create}
hook {on_fight_start} {create}

#class {fight} {open}

#var {combat_status} {idle}
#var {combat_hint} {}
#var {currently_fighting} {}
#var {currently_assisting} {}
  
#var {fighting_words} {annihilates|atomizes|beats the ever-living shit out of|bites|cleaves|channels|claws|Claws|crushes|decimates|demolishes|devastates|ethereally touches|gashes|grapples with|hits|hoofs|jabs|lashes|leaps upon|massacres|misses|mutilates|obliterates|pierces|pinches|pulverizes|punches|rocks|saws|smashes|stings|stuns|tangles|thrashes|uppercuts|vanquishes|whips}
#var {you_fighting_words} {annihilate|atomize|bite|channel|claw|cleave|crushe|decimate|demolish|devastate|grapple with|gash|hit|leap upon|massacre|mutilate|obliterate|pierce|pinch|pulverize|punch|rock|thrash|sting|stun|vanquish|whip}
#var {you_beat_words} {beat the ever-living shit out of}

#action {You {$you_fighting_words} %2 {with|hard|very}} {update_fight {%2}}
#action {You {$you_beat_words} %2.} {update_fight {%2}}
#action {%1 {$fighting_words} you} {update_fight {%1}}
#action {%1 sees you, and attacks!} {update_fight {%1}}
#action {You dodge %1's} {update_fight {%1}}
#action {Your fire shield flares about %1 and you have} {update_fight {%1}}
#action {You are pissed and attack %1!} {update_fight {%1}} {9}

#action {in the back of %1, resulting in several strange noises and some blood.} {update_fight {%1}}
#action {in the back of %1, who suddenly slumps to the ground, spastically shaking before stiffening as a lifeless corpse.} {update_fight {%1}}

#action {Maybe you should get on your feet first?} {stand}
#action {You can't do this sitting!} {stand}

#action {%1 leaps to your rescue, you are confused!} {
  #var {combat_status} {idle};
  #var {combat_hint} {};
  maybe_assist {%1} {$currently_fighting}
}

#action {You are rescued by %1, you are confused!} {
  #var {combat_status} {idle};
  #var {combat_hint} {};
  maybe_assist {%1} {$currently_fighting}
}

#action {%1 disappears.} {fight_mob_gone}
#action {sends %1 flying from the room!} {fight_mob_gone}

#alias {fight_mob_gone} {
  #if {"$combat_status" != "idle"} {
    debug MOB GONE!;  
    hook on_prompt {once} {fight_mob_gone} {
      end_fight {$currently_fighting} {mob-gone}
    }
  }
}

#action {R.I.P} {
  debug RIP ACTION --> dispatch through prompt;
  hook on_prompt {once} {after_rip} {
    debug RIP ACTION --> prompt triggered --> end_fight;
    end_fight {$currently_fighting} {rip}
  }
}

#alias {reset_fight_timer} {
  #undelay {fighting_timeout};
  #delay {fighting_timeout} {fighting_timed_out} {5}
}

#alias {fight_hint} {
  #var {combat_hint} {assisted}
};

#alias {update_fight} {
  reset_fight_timer;
  #if {"$combat_status" == "idle"} {
    debug BEGIN FIGHT;
    #var {combat_status} {fighting};
    #var {currently_fighting} {%1};
    hook on_fight_start {fire} {%1}
  }
}

#alias {maybe_assist} {
  reset_fight_timer;
  #if {"$combat_status" == "idle"} {
    debug BEGIN ASSIST {%1} {%2};
    #var {combat_status} {fighting};
    #var {combat_hint} {};
    #var {currently_fighting} {%2};
    #var {currently_assisting} {%1};
    do_fight_assist {%1} {%2}
  }
}

#alias {end_fight} {
  debug END FIGHT status=$combat_status hint=$combat_hint;
  #if {"$combat_status" == "fighting" || "$combat_hint" != ""} {
    #var {combat_status} {idle};
    #var {combat_hint} {};
    #var {currently_fighting} {};
    #var {currently_assisting} {};
    debug END FIGHT will emit RIP after short delay;
    #undelay {fighting_timeout};
    #delay {fighting_timeout} {
      #var {fight_last_mob} {%1};
      #var {fight_end_reason} {%2};
      hook {on_rip} {exec-each} {#nop} {#nop}
    } {0.1}
  }
  { debug END-FIGHT --> skip }
}

#alias {fighting_timed_out} {
  debug FIGHT TIMED OUT (bug?);
  end_fight {$currently_fighting}
}

#alias {add_fight_hint} {
  #action {%1 slowly fades into existence.} {fight_hint}
}

#alias {add_group_fight_hints} {
  #class {fight} {open};
  #foreach {*group_stats[]} {player} {  
    add_fight_hint {$player}
  };
  #class {fight} {save};
  #class {fight} {close}  
}

#class {fight} {save}
#class {fight} {close}