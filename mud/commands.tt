#class {commands} {open}

#var {target} {}

#alias {cmd_noop} {
  exec_task {cmd_noop} {#show cmd_noop} {%1} {%2} {
    #action {cmd_noop} {trampoline {cmd_noop} {task_cmd_noop_done}}
  }
}

#alias {cmd_agg} {
  exec_task {cmd_agg} {agg} {%1} {%2} {
    #action {You are now aggressive towards all aggressive monsters, and will attack first!} {task_cmd_agg_done};
    #action {You are not aggressive towards aggressive monsters anymore.} {task_cmd_agg_attempt}
  }
}

#alias {cmd_unagg} {
  exec_task {cmd_unagg} {agg} {%1} {%2} {
    #action {You are now aggressive towards all aggressive monsters, and will attack first!} {task_cmd_unagg_attempt};
    #action {You are not aggressive towards aggressive monsters anymore.} {task_cmd_unagg_done}
  }
}

#alias {cmd_pick} {
  exec_task {cmd_pick} {pi %1} {%2} {%3} {
    #var {task_cmd_pick[attempts]} {50};
    #action {The lock quickly yields to your skills.} {task_cmd_pick_done};
    #action {I see no %1 here.} {task_cmd_pick_fail};
    #action {Oh.. it wasn't locked at all.} {task_cmd_pick_done};
    #action {You failed to pick the lock.} {task_cmd_pick_attempt};
    #action {You realize that the door is already open.} {task_cmd_pick_done}
  }
}

#alias {cmd_open_door} {
 exec_task {cmd_open_door} {open %1} {%2} {%3} {
   #action {Ok.} {task_cmd_open_door_done};
   #action {It's already open!} {task_cmd_open_door_done};
   #action {Heck.. it ain't even closed!} {task_cmd_open_door_done};
   #action {I see no %1 here.} {task_cmd_open_door_fail};
   #action {The lock quickly yields to your skills.} {task_cmd_open_door_fail};
   #action {That's not a container.} {task_cmd_open_door_fail}
 }
}

#alias {cmd_unlock_door} {
  cmd_unlock_door_with_key {get %1} {%2} {drop all.%1} {%3} {%4}
}

#alias {cmd_unlock_door_with_key} {
 exec_task {cmd_unlock_door_with_key} {%1; unlock %2; %3} {%4} {%5} { 
   #action {You do not have the proper key for that.} {
     #delay {unlock_door_with_key} {task_cmd_unlock_door_with_key_attempt} {1.0}
   };
   #action {*click*} {task_cmd_unlock_door_with_key_done};
   #action {It's already unlocked, it seems.} {task_cmd_unlock_door_with_key_done};
   #action {Heck.. it ain't even closed!} {task_cmd_unlock_door_with_key_done};

   #alias {task_cmd_unlock_door_with_key_end_hook} {
     #undelay {unlock_door_with_key}
   }
 }  
}

#alias {cmd_close_door} {
  exec_task {cmd_close_door} {close %1} {%2} {%3} {
    #action {Ok.} {task_cmd_close_door_done};
    #action {I see no %1 here.} {task_cmd_close_door_fail};
    #action {It's already closed!} {task_cmd_close_door_done}
  }
}

#alias {cmd_wiggle} {
  exec_task {cmd_wiggle} {wiggle} {%1} {%2} {
    #action {Your wiggle} {task_cmd_wiggle_done}
  }
}

#alias {cmd_look_room} {
  exec_task {cmd_look_room} {look} {%2} {%3} {
    #action {%1} {task_cmd_look_room_done};
    #action {Exits:} {task_cmd_look_room_fail}
  }
}

#alias {reset_target} {debug TARGET --> {%1}; #var {target} {%1}}
#alias {cmd_set_target} {
  exec_task {cmd_set_target} {#show SET-TARGET: %1} {%2} {%3} {
    #action {SET-TARGET: %%%1} {reset_target {%%%1}; task_cmd_set_target_done}
  }
}

#alias {cmd_recall} {
  exec_task {cmd_recall} {look} {%1} {%2} {
    #action {Inn of the Flying Circus} {task_cmd_recall_done};
    #action {Exits:} {rec recall-scroll};
    #action {Zifnab tells you 'No magic here - kid!'} {task_cmd_recall_fail};
    #action {You do not have that item.} {
      cmd_recall_fallback
        {task_cmd_recall_done}
        {task_cmd_recall_fail}
    }
  }
}

#alias {cmd_ponder} {
  exec_task {cmd_ponder} {ponder} {%1} {%2} {
    #action {You ponder the situation.} {task_cmd_ponder_done}
  }
}

#alias {cmd_stand} {
  exec_task {cmd_stand} {stand} {%1} {%2} {
    #action {stand up.} {task_cmd_stand_done};
    #action {You are already standing.} {task_cmd_stand_done}
  }
}

#alias {cmd_walk} {
  debug ACTION-WALK {%1} -- {%2};
  exec_task {cmd_walk} {%1} {%3} {%4} {
    #action {Alas, you cannot go that way...} {task_cmd_walk_fail};
    
    #action {Sorry, there is no room to get in there.} {
      tell group 'WALK: no room, trying again in a moment;
      #delay {cmd_walk_wait} {task_cmd_walk_attempt} {2.0}
    };
    
    #action {%2} {debug WALK-FAST DONE; task_cmd_walk_done};
    
    #action {Nah... You feel too relaxed to do that..} {stand; task_cmd_walk_attempt};

    #alias {task_cmd_walk_end_hook} {
      #undelay {cmd_walk_wait}
    }
  }
}

#alias {cmd_run} {
  debug ACTION-RUN {%1} -- {%2};
  exec_task {cmd_run} {run %1} {%3} {%4} {
    #action {Do you like to run into things doofus?} {task_cmd_run_fail};    
    #action {%2} {debug CMD-RUN DONE; task_cmd_run_done};    
    #action {Nah... You feel too relaxed to do that..} {stand; task_cmd_run_attempt};
  }
}

#alias {cmd_autoloot} {
  exec_task {cmd_autoloot} {autoloot %1} {%2} {%3} {
    debug SETTING AUTOLOOT --> %1;
    #action {Autoloot what?!} {task_cmd_autoloot_fail};
    #action {Autoloot is now ON.} {#if {"%1" == "on"} {task_cmd_autoloot_done} {task_cmd_autoloot_fail}};
    #action {Autoloot is now OFF.} {#if {"%1" == "off"} {task_cmd_autoloot_done} {task_cmd_autoloot_fail}};
    #action {Autolooting GOLD only.} {#if {"%1" == "gold"} {task_cmd_autoloot_done} {task_cmd_autoloot_fail}}
  }
}

#alias {cmd_wait_here} {
  exec_task {cmd_wait_here} {rest} {%1} {%2} {
    #action {You sit down and rest your tired bones.} {task_cmd_wait_here_done};
    #action {You are already resting.} {task_cmd_wait_here_done}
  }  
}

#alias {cmd_give_all} {
  exec_task {cmd_give_all} {give all.%1 %2} {%3} {%4} {
    #action {You do not seem to have anything like that.} {task_cmd_give_all_fail};
    #action {You give} {task_cmd_give_all_done}
  }
}

#alias {cmd_give_coins} {
  exec_task {cmd_give_coins} {give %1 coins %2} {%3} {%4} {
    #action {You do not seem to have anything like that.} {task_cmd_give_coins_fail};
    #action {You haven't got that many coins!} {task_cmd_give_coins_fail};
    #action {can not possibly carry any more coins!} {task_cmd_give_coins_fail};
    #action {They aren't here.} {task_cmd_give_coins_fail};
    #action {Ok.} {task_cmd_give_coins_done}
  }
}

#alias {cmd_score} {
  #class {score} {open};

  #var {score_max_hit} {0};
  #var {score_max_mana} {0};
  #var {score_max_movement} {0};
  #var {score_gold_coins} {0};
  #var {score_alignment} {0};
  #var {score_exp} {0};
  #var {score_description} {};
  #var {score_levels} {};

  #var {max_mana} {0};
  #var {max_refresh} {0};   
  #var {max_hp} {0};

  #class {score} {save};
  #class {score} {close};

  exec_task {cmd_score} {score} {%2} {%3} {
    #var {score_levels} {};
    
    #action {You have %%%+(%%%+) hit, %%%+(%%%+) mana and %%%+(%%%+) movement points.} {  
      #var {health} {%%%1}; #var {score_max_hit} {%%%2}; #var {max_hp} {%%%2};
      #var {mana} {%%%3}; #var {score_max_mana} {%%%4}; #var {max_mana} {%%%4};
      #var {movement} {%%%5}; #var {score_max_movement} {%%%6}; #var {max_refresh} {%%%6}
    };

    #action {Your alignment (-1000 : +1000): %%%+.} {
      #var {score_alignment} {%%%1}
    };

    #action {Your levels:%%%s%%%S:%%%S} {
      #var {score_levels[%%%2]} {%%%3}
    };

    #action {You have scored %%%+ exp, and have %%%+ gold coins.} {
      #var {score_exp} {%%%1};    
      #var {score_gold_coins} {%%%2}
    };

    #action {This ranks you as %%%S} {
      #var {score_description} {%%%1}
    };

    #action {You have been playing for} {%1; task_cmd_score_done}    
  }
}

#alias {cmd_group_stats} {
  #class {group} {open};

  #var {group_stats} {};
  #var {group_leader} {};
 
  #class {group} {save};
  #class {group} {close};
  
  exec_task {cmd_group_stats} {group} {%1} {%2} {
    #action {Your group consists of:} {
      hook {on_prompt} {once} {cmd_group_stats} {
        trampoline {cmd_group_stats} {task_cmd_group_stats_done}
      }
    };

    #action {Leader: %%%w%%%s[%%%+,%%%+,%%%+]} {
      #var {group_leader} {%%%1};
      #var {group_stats[%%%1]} {{hit}{%%%3}{mana}{%%%4}{move}{%%%5}}
    } {4};

    #action {%%%w%%%s[%%%+,%%%+,%%%+]} {
      #var {group_stats[%%%1]} {{hit}{%%%3}{mana}{%%%4}{move}{%%%5}}
    }
  }
}

#alias {cmd_align_check} {
  #alias {assert_align} {
    #if {$score_alignment > 500} {
      debug CHECK-ALIGN: ok alignment=$score_alignment;
      %1
    } {
      debug CHECK-ALIGN: too-low alignment=$score_alignment;
      %2 {alignment=$score_alignment}
    }    
  };
  
  cmd_score {trampoline {cmd_align_check} {assert_align}}
}

#alias {cmd_group_score} {
  cmd_score {trampoline {cmd_group_score} {cmd_group_stats {%1} {%2}}}
}

#alias {cmd_wait_for_room} {
  exec_task {cmd_wait_for_room}
    {look}
    {debug FOUND ROOM; %3}
    {debug NO ROOM FOUND; %4}
    {
      #action {%1} {task_cmd_wait_for_room_done};
      #action {Exits:} {task_cmd_wait_for_room_reset_timeout; %2} {6}
    }
}

#alias {cmd_teleport_room} {
  exec_task {cmd_teleport_room}
    {#tick {teleport_room} {look} {1}; look}
    {debug FOUND TELEPORT ROOM; %2}
    {debug NO TELEPORT ROOM FOUND; %3}
    {
      #action {%1} {task_cmd_teleport_room_done};
      #alias {task_cmd_teleport_room_end_hook} {
        #untick {teleport_room}
      }
    }
}

#alias {cmd_wait_for_time} {
  exec_task {cmd_wait_for_time} {wait_for_time_delay} {%2} {%3} {
    #alias {wait_for_time_delay} {
      debug WAIT FOR TIME --> %1;
      #delay {wait_for_time_delay} {#show DONE cmd_wait_for_time} {%1}
    };
    #action {DONE cmd_wait_for_time} {task_cmd_wait_for_time_done}
  }
}

#alias {cmd_layhands} {
  exec_task {cmd_layhands} {lay %1} {%2} {%3} {
    #action {You never did get First Aid Merit Badge did you?} {task_cmd_layhands_fail};
    #action {You lay hands on} {task_cmd_layhands_done};
    #action {Don't try to heal what ain't hurt!} {task_cmd_layhands_done};
    #action {You don't seem to be up to healing at the moment.} {task_cmd_layhands_fail};
    #nop TODO -- not enough mana
  } {15}
}

#alias {cmd_bless} {
  exec_task {cmd_bless} {bless %1} {%2} {%3} {
    #action {You bless} {task_cmd_bless_done};
    #action {You fail in the bestowal of your blessing} {task_cmd_bless_fail};
    #action {WHO do you wish to bless?} {task_cmd_bless_fail}
  } {15}
}

#alias {cmd_ask_for_heal} {
  exec_task {cmd_ask_for_heal} {tell_robot healme} {%1} {%2} {
    #action {tells you 'heal failed} {debug ASK-FOR-HEAL: fail; task_cmd_ask_for_heal_fail};
    #action {tells you 'cure critic failed} {debug ASK-FOR-HEAL: fail; task_cmd_ask_for_heal_fail};
    #action {You feel better!} {task_cmd_ask_for_heal_done};
    #action {A strange cold fire seizes your heart} {task_cmd_ask_for_heal_done};
    #action {lays hands on you.} {task_cmd_ask_for_heal_done}
  }
}

#alias {cmd_ask_for_refresh} {
  exec_task {cmd_ask_for_refresh} {tell_robot cast second wind} {%1} {%2} {
    #action {tells you 'second wind failed} {task_cmd_ask_for_refresh_fail};
    #action {You feel less tired.} {task_cmd_ask_for_refresh_done}
  }
}

#alias {cmd_ask_for_cast} {
  exec_task {cmd_ask_for_cast} {tell_robot cast %1} {%2} {%3} {
    #action {tells you 'OK: cast %1} {task_cmd_ask_for_cast_done};
    #action {tells you 'FAIL: cast %1} {task_cmd_ask_for_cast_fail}
  }
}

#alias {cmd_check_container} {
  exec_task {cmd_check_container} {look in %1} {%3} {%4} {
    #action {You do not see that item here.} {
      debug CONTAINER-CHECK: NO CONTAINER {%1};
      task_cmd_check_container_fail
    };

    #action {(carried) :} {
      hook {on_prompt} {once} {cmd_check_container} {
        debug CONTAINER-CHECK: MISSING {%2};
        #alias {on_prompt} {#nop};
	task_cmd_check_container_fail
      }
    };
    
    #action {%2} {
      debug CONTAINER-CHECK: FOUND {%2};
      hook {on_prompt} {remove} {cmd_check_container};
      task_cmd_check_container_done
    }
  } 
}

#alias {cmd_check_exit} {
  exec_task {cmd_check_exit} {exits} {%2} {%3} {
    #action {%1} {
      debug CHECK-EXIT-FOUND;
      hook {on_prompt} {remove} {cmd_check_exit};
      task_cmd_check_exit_done
    };
    
    #action {Obvious exits:} {
      hook {on_prompt} {once} {cmd_check_exit} {
	debug CHECK-EXIT-PROMPT-FAIL;
	task_cmd_check_exit_fail
      }
    }
  }
}

#function {deposit_target_coins} {
  #var {result} {600000}
}

#alias {cmd_deposit} {
  exec_task {cmd_deposit} {#show GIVE-DEPOSIT} {%1} {%2} {
    #var {deposit_coins} {0};
    #var {withdraw_coins} {0};
    #var {target_coins} {@deposit_target_coins{}};
    
    #action {GIVE-DEPOSIT} {
      #math {deposit_coins} {$score_gold_coins - $target_coins};
      #math {withdraw_coins} {$target_coins - $score_gold_coins};
      #if {$deposit_coins > 0} {
        debug DEPOSIT (ok) deposit=$deposit_coins total=$score_gold_coins;
        deposit $deposit_coins
      }; #elseif {$withdraw_coins > 0} {
        debug WITHDRAW (ok) withdraw=$withdraw_coins total=$score_gold_coins;
	withdraw $withdraw_coins
      }; #else {        
        debug SKIP BANK deposit=$deposit_coins withdraw=$withdraw_coins total=$score_gold_coins;
	task_cmd_deposit_done
      }
    };
    #action {Thank you.} {task_cmd_deposit_done}
  }
}

#alias {cmd_farlook} {
  exec_task {cmd_farlook} {farlook %1} {%3} {%4} {
    #if {"%2" == "Immediately"} {
      #action {Immediately %%%1, you spot %%%2.} {task_cmd_farlook_done} {4};
      #action {you spot %%%1.} {task_cmd_farlook_fail {not-nearby}}
    }; #elseif {"%2" == "Nearby"} {
      #action {{Immediately|Nearby} %%%2, you spot %%%3.} {task_cmd_farlook_done} {4};    
      #action {you spot %%%1.} {task_cmd_farlook_fail {not-nearby}}
    }; #else {
      #action {you spot %%%1.} {task_cmd_farlook_done}
    };
    
    #action {You peer around, but can't find anyone of that description.} {task_cmd_farlook_fail {not-found}};
    #action {You see %%%1 right here.} {task_cmd_farlook_fail {here}};
    #action {No way! You are fighting for your life!} {task_cmd_farlook_fail {fighting}} {4}
  }
}

#var {scan_dir} {0}
#alias {scan_dir_add} {#math {scan_dir} {$scan_dir + 1}}
#alias {cmd_scan_dir} {
  exec_task {cmd_scan_dir} {scan %1} {%3} {%4} {
    #action {You peer intently} {scan_dir_begin};    

    #if {"%2" == "Immediately"} {
      #action {%%%1 : {immediately} {to the|upwards|downwards}} {scan_dir_add {%%%1} {%%%2}};
    }; #elseif {"%2" == "Nearby"} {
      #action {%%%1 : {immediately|nearby} {to the|upwards|downwards}} {scan_dir_add {%%%1} {%%%2}};
    }; #else {
      #action {%%%1 : {immediately|nearby|a ways} {to the|upwards|downwards}} {scan_dir_add {%%%1} {%%%2}};
    };

    #action {No way! You are fighting for your life!} {task_cmd_scan_dir_fail {fighting}} {4};

    #alias {scan_dir_begin} { 
      #var {scan_dir} {0};
      hook {on_prompt} {once} {scan_dir_begin} {scan_dir_end}
    };

    #alias {scan_dir_end} {
      #if {$$scan_dir > 0} {
        debug SCAN-DIR found=$$scan_dir;
        task_cmd_scan_dir_done
      } {
        task_cmd_scan_dir_fail {not-found}
      }
    }    
  }
}

#alias {cmd_drop_all} {
  exec_task {cmd_drop_all} {drop all.%1} {%2} {%3} {
    #action {You drop} {task_cmd_drop_all_done};
    hook {on_prompt} {once} {cmd_drop_all} {task_cmd_drop_all_done};
    #alias {task_cmd_drop_all_end_hook} {hook {on_prompt} {remove} {cmd_drop_all}}
  }
}

#alias {cmd_shieldblock} {
  exec_task {cmd_shieldblock} {shieldblock %1} {%2} {%3} {
    #action {Shield Block:} {task_cmd_shieldblock_done}
  }
}

#alias {cmd_imm_transition} {
  exec_task {cmd_imm_%1_%2} {imm_transition {%1} {%2}} {%4} {%5} {
    #action {%3} {task_cmd_imm_%1_%2_done};
    #action {IMM ALREADY %1 --> %2} {task_cmd_imm_%1_%2_done};
    #action {ALREADY WEARING} {task_cmd_imm_%1_%2_done}    
  }
}

#var {count_container_found} {0}
#alias {cmd_count_container} {
  exec_task {cmd_count_container} {look in %1} {%3} {%4} {  
  
    #action {You do not see that item here.} {
      debug CONTAINER-CHECK: NO CONTAINER {%1};
      #var {count_container_found} {0};
      task_cmd_count_container_fail
    };

    #action {(carried) :} {
      hook {on_prompt} {once} {cmd_count_container} {count_container_prompt}
    };

    #alias {count_container_prompt} {
      debug CONTAINER-CHECK: MISSING {%2};
      #var {count_container_found} {0};
      task_cmd_count_container_fail
    };
    
    #action {%2} {
      debug CONTAINER-CHECK: FOUND {%2};
      #var {count_container_found} {1};
      task_cmd_count_container_done
    } {6};

    #action {%2%%%1[%%%2]} {
      debug CONTAINER-CHECK: FOUND {%2};
      #var {count_container_found} {%%%2};
      task_cmd_count_container_done
    };

    #alias {task_cmd_count_container_end_hook} {
      hook {on_prompt} {remove} {cmd_count_container}
    }
  } 
}

#var {count_inventory_found} {0}
#alias {cmd_count_inventory} {
  exec_task {cmd_count_inventory} {inventory} {%2} {%3} {  
  
    #action {You are carrying:} {
      hook {on_prompt} {once} {cmd_count_inventory} {count_inventory_prompt}
    };

    #alias {count_inventory_prompt} {
      debug INVENTORY-CHECK: MISSING {%1};
      #var {count_inventory_found} {0};
      task_cmd_count_inventory_fail
    };
    
    #action {%1} {
      debug INVENTORY-CHECK: FOUND {%1};
      #var {count_inventory_found} {1};
      task_cmd_count_inventory_done
    } {6};

    #action {%1[%%%1]} {
      debug INVENTORY-CHECK: FOUND {%1};
      #var {count_inventory_found} {%%%1};
      task_cmd_count_inventory_done
    };

    #alias {task_cmd_count_inventory_end_hook} {
      hook {on_prompt} {remove} {cmd_count_inventory}
    }
  } 
}

#alias {cmd_recite_from_on} {
  exec_task {cmd_recite_from_on} {get %1 %2} {%4} {%5} {
    #alias {do_recite} {recite %1 %3};    
    #action {You do not see a %1} {debug RECITE: no %1 in %2 --> fail; task_cmd_recite_from_on_fail};
    #action {You get} {debug RECITE: got %1 --> recite; do_recite};
    #action {which bursts into flame} {debug RECITE --> done; task_cmd_recite_from_on_done};
    #action {You do not have that item.} {debug RECITE: no %1 --> fail; task_cmd_recite_from_on_fail};
    #action {No such thing around to recite the scroll on.} {
      debug RECITE: no %3 --> fail;
      put %1 %2;
      task_cmd_recite_from_on_fail
    }
  }
}

#nop usage -- cmd_throw_dagger_from_container {fish} {$target} {golden-case} {frozen fish} {debug YAY} {debug BOO}
#alias {cmd_throw_dagger_from_container} {
  exec_task {cmd_throw_dagger_from_container} {throw %1 %2} {%5} {%6} {
    #action {You don't have that!} {get %1};
    #action {You do not see a %1 here.} {get %1 %3};
    #action {You do not see or have} {task_cmd_throw_dagger_from_container_fail {no-container}};
    #action {You do not see a %1 in} {task_cmd_throw_dagger_from_container_fail {no-dagger}};
    #action {You get a %4} {throw %1 %2};
    #action {You throw a %4} {task_cmd_throw_dagger_from_container_done};
    #action {No way! You are fighting for your life!} {task_cmd_throw_dagger_from_container_fail {fighting}};
    #action {You don't see anybody around} {task_cmd_throw_dagger_from_container_fail {no-mob}};
  }
}

#alias {cmd_portal} {
  exec_task {cmd_portal} {tell group 'dothis: %1} {%2} {%3} {
    #action {a portal appears!} {task_cmd_portal_done}
  }
}

#alias {cmd_attack} {
  exec_task {cmd_attack} {attack %1} {%3} {%4} {
    #action {attack name: %%%1(%%%2)} {
      debug ATTACK %%%1 is %%%2;
      #if {"%%%2" == "%2"} {task_cmd_attack_done} {task_cmd_attack_attempt}
    }
  }
}

#var {all_attack_forms} {}
#alias {cmd_attack_forms} {
  exec_task {cmd_attack_forms} {attacks} {%1} {%2} {
    #action {Special Attack forms} {
      #var {all_attack_forms} {};
      hook {on_prompt} {once} {cmd_attack_forms} {
	trampoline {cmd_attack_forms} {task_cmd_attack_forms_done}
      }
    };

    #action {[%%%+] %%%+(%%%+)} {
      #var all_attack_forms[%%%1] {%%%3}
    };
  }
}

#alias {cmd_set_all_attacks} {
  cmd_attack_forms {continue_with_attacks {%1} {%2}} {%3}
}

#alias {continue_with_attacks} {
  #local {want_attacks} {};
  #local {want} {};
  #foreach {%1} {want} {
    #var {want_attacks[$want]} {active}
  };

  #local {change_attacks} {};
  #local {have} {};
  #foreach {*all_attack_forms[]} {have} {
    #if {"$want_attacks[$have]" != "" && "$want_attacks[$have]" != "$all_attack_forms[$have]"} {
      #var {change_attacks[$have]} {$want_attacks[$have]}
    };
    #elseif {"$all_attack_forms[$have]" == "active" && "$want_attacks[$have]" != "active"} {
      #var {change_attacks[$have]} {inactive}
    }
  };

  #if {"$change_attacks" != ""} {
    debug OK! CHANGE THESE ATTACKS $change_attacks;
    #local {change} {};
    #foreach {*change_attacks[]} {change} {
      attack $change
    }
  };

  trampoline {continue_with_attacks} {%2}
}

#alias {cmd_aura} {
  exec_task {cmd_aura} {aura} {%1} {%2} {
    #action {Your eyes glow briefly and you adjust to the new auras.} {aura_up};
    #action {You already have partial aura sight.} {aura_up};

    #alias {aura_up} {
      spell_state_up {detect magic};
      task_cmd_aura_done
    }
  }
}

#alias {cmd_great_sight} {
  exec_task {cmd_great_sight} {great} {%1} {%2} {
    #action {You succede in enhancing your vision.} {great_sight_up};
    #action {You already have partial great sight.} {great_sight_up};

    #alias {great_sight_up} {
      spell_state_up {true sight};
      task_cmd_great_sight_done
    }
  }
}

#alias {cmd_get_corpse} {
  exec_task {cmd_get_corpse} {carve corpse} {%3} {%4} {
    #action {You carve up a nice fat slice.} {carve corpse};
    #action {There is no good meat left on it.} {get corpse};
    #action {You get the corpse of %1} {safe_put corpse %2} {4};
    #action {You get the corpse} {junk corpse; carve corpse} {5};
    #action {You put the corpse} {task_cmd_get_corpse_done};
    #action {It won't fit} {task_cmd_get_corpse_fail};
    #action {You do not see a corpse here.} {task_cmd_get_corpse_fail};
    #action {That's not here.} {task_cmd_get_corpse_fail}    
  }
}

#class {commands} {save}
#class {commands} {close}