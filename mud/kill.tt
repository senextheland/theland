#class {kill} {open}

#alias {kill_room} {
  #class {kill_room} {open};

  #var {kill_room_targets} {%1};
  #alias {kill_room_done} {kill_room_end; %2};
  
  #list {kill_room_targets} {explode} {;};
  #var {kill_room_target} {};
      
  #var {kill_room_next_target} {0};

  #alias {kill_room_attack} {
    reset_zone_timeout;
    kill $kill_room_target
  };

  #alias {kill_room_next_target} {
    #list {kill_room_targets} {get} {1} {kill_room_target};
    #if {"$combat_status" != "idle"} {
      debug KILL-ROOM: skip next target: combat_status=$combat_status
    }; #elseif { "$kill_room_target" == "0" } {
      kill_room_done
    }; #else {
      #list {kill_room_targets} {delete} {1};
      kill_room_attack
    }
  };

  #alias {kill_room_hands_full} {
    debug KILL-ROOM: hands full --> reset zone timeout;
    update_fight {}
  };

  #action {They aren't here.} {kill_room_next_target};
  #action {You do the best you can!} {kill_room_hands_full};
  #action {You have your hands full right now} {kill_room_hands_full};
  hook {on_rip} {add} {kill_room} {
    trampoline {kill_room} {kill_room_attack};
    %%1
  };

  #alias {kill_room_end} {
    hook {on_rip} {remove} {kill_room};
    #class {kill_room} {kill}
  };

  #class {kill_room} {save};
  #class {kill_room} {close};

  kill_room_next_target
}

#alias {kill_walk_path_end} {#nop}
#alias {kill_walk_path} {
  #class {kill_walk_path} {open};

  #var {kill_walk_path} {%1};
  #var {kill_walk_path_target} {%2};
  #alias {kill_walk_path_done} {kill_walk_path_end; %3};
  #alias {kill_walk_path_fail} {kill_walk_path_end; %4};

  #alias {kill_walk_path_end} {
    hook {before_walk_room} {remove} {kill_walk_path};
    #class {kill_walk_path} {kill};
    #alias {kill_walk_path_end} {#nop}
  };

  #alias {kill_walk_room} {
    kill_room {$kill_walk_path_target} {step}
  };
  
  #class {kill_walk_path} {save};
  #class {kill_walk_path} {close};

  hook {before_walk_room} {add} {kill_walk_path} {zone_continuation};
  walk_each {$kill_walk_path} {kill_walk_room} {kill_walk_path_done} {kill_walk_path_fail};
  step
}

#class {kill} {save}
#class {kill} {close}
