#class {dagger} {open}

#alias {dagger_mode} {fish}

#alias {dagger_all_end} {#nop}
#alias {dagger_all} {
  #class {dagger_all} {open};

  #list {dagger_all_targets} {create} {%1};  
  #alias {dagger_all_done} {dagger_all_end; %2};
  #alias {dagger_all_fail} {dagger_all_end; %3};
  
  #var {dagger_all_current_target} {};
  #alias {dagger_all_end} {
    hook {on_rip} {remove} {dagger_all};
    fish_end;
    #class {dagger_all} {kill}
  };

  #alias {dagger_all_attack} {
    zone_continuation {dagger_all_attack_after_continuation}
  };

  #alias {dagger_all_attack_after_continuation} {
    #var {dagger_all_current_target};
    dagger_mode {$dagger_all_current_target}
      {trampoline {dagger_all_attack} {dagger_all_attack}}
      {dagger_all_attack_fail}
  };

  #alias {dagger_all_attack_fail} {
    debug DAGGER-ALL-FAIL: {%%1};
    #switch {"%%1"} {
      #case {"fighting"} {dagger_all_fighting};
      #case {"no-dagger"} {dagger_all_fail {no-dagger}};
      #case {"no-mob"} {dagger_all_next};
      #default {dagger_all_next}
    }   
  };

  #alias {dagger_all_fighting} {
    debug DAGGER-ALL: fighting --> nop;
    fish_end
  };

  hook {on_rip} {add} {dagger_all} {
    trampoline {dagger_all} {debug TRAMPOLINE --> attack; dagger_all_attack};
    %%1
  };

  #alias {dagger_all_next} {    
    #list {dagger_all_targets} {get} {1} {dagger_all_current_target};
    #if { "$dagger_all_current_target" == "0" } {
      dagger_all_done
    } {
      #list {dagger_all_targets} {delete} {1};
      dagger_all_attack
    }
  };

  #class {dagger_all} {save};
  #class {dagger_all} {close};

  dagger_all_next
}

#alias {dagger_walk_end} {#nop}
#alias {dagger_walk_path} {
  #class {dagger_walk_path} {open};

  #var {dagger_walk_path} {%1};
  #var {dagger_walk_target} {%2};
  #alias {dagger_walk_done} {dagger_walk_end; %3};
  #alias {dagger_walk_fail} {dagger_walk_end; %4};

  #alias {dagger_walk_kill_room} {
    kill_room {$dagger_walk_target}
      {dagger_walk_kill_room_done}
  };

  #alias {dagger_walk_kill_room_done} {
    debug DAGGER-WALK-KILL-ROOM-DONE --> farlook;
    exec {dagger_walk_kill_room_done} {cmd_farlook $dagger_walk_target {}}
      {dagger_walk_farlook_found}
      dagger_walk_farlook_fail
  };

  #alias {dagger_walk_farlook_found} {
    debug DAGGER-WALK-FARLOOK-FOUND;
    dagger_all {$dagger_walk_target}
      {dagger_walk_throw_done}
      {dagger_walk_throw_fail %%0}
  };

  #alias {dagger_walk_farlook_fail} {
    debug DAGGER-WALK-FARLOOK-FAIL --> %%1;
    #switch {"%%1"} {
      #case {"here"} {dagger_walk_kill_room};
      #case {"not-found"} {step};
      #default {debug dagger_walk_farlook_fail UNRECOGNIZED COMMAND {%%1}}
    }    
  };

  #alias {dagger_walk_throw_done} {
    debug DAGGER-WALK-THROW-DONE --> step;
    step
  };

  #alias {dagger_walk_throw_fail} {
    debug DAGGER-WALK-THROW-FAIL: %%0;
    #switch {"%%1"} {
      #case {"no-mob"} {dagger_walk_throw_done};
      #default {dagger_walk_fail %%0}
    }
  };

  #class {dagger_walk_path} {save};
  #class {dagger_walk_path} {close};

  #alias {dagger_walk_end} {
    #class {dagger_walk_path} {kill};
    #alias {dagger_walk_end} {#nop}
  };

  walk_each {%1} {dagger_walk_kill_room} {dagger_walk_done} {dagger_walk_fail};
  dagger_walk_kill_room
}

#alias {dagger_nearby} {
  #class {dagger_nearby} {open};

  #var {dagger_nearby_target} {%1};
  #var {dagger_nearby_range} {%2};
  #alias {dagger_nearby_done} {dagger_nearby_end; %3};
  #alias {dagger_nearby_fail} {dagger_nearby_end; %4};

  #alias {dagger_nearby_end} {
    hook {on_rip} {remove} {dagger_nearby};
    #class {dagger_nearby} {kill}    
  };

  #alias {dagger_nearby_farlook} {
    exec {dagger_nearby_farlook} {cmd_farlook $dagger_nearby_target {$dagger_nearby_range}}
      {dagger_nearby_farlook_found}
      dagger_nearby_farlook_fail  
  };

  #alias {dagger_nearby_farlook_found} {
    debug DAGGER-NEARBY-FARLOOK-FOUND;
    dagger_nearby_attack
  };

  #alias {dagger_nearby_farlook_fail} {
    debug DAGGER-NEARBY-FARLOOK-FAIL --> %%1;
    #switch {"%%1"} {
      #case {"here"} {dagger_walk_kill_room};
      #case {"not-found"} {dagger_nearby_done};
      #case {"not-nearby"} {dagger_nearby_done};
      #default {debug dagger_nearby_farlook_fail UNRECOGNIZED COMMAND {%%1}}
    }  
  };

  #alias {dagger_nearby_kill_room} {
    kill_room {$dagger_nearby_target}
      {dagger_nearby_farlook}
  };

  #alias {dagger_nearby_throw_done} {
    debug DAGGER-NEARBY-THROW-DONE --> step;
    dagger_nearby_done
  };

  #alias {dagger_nearby_throw_fail} {
    debug DAGGER-NEARBY-THROW-FAIL: %%0;
    #switch {"%%1"} {
      #case {"no-mob"} {dagger_nearby_throw_done};
      #default {dagger_nearby_fail %%0}
    }
  };

  #alias {dagger_nearby_attack} {
    zone_continuation {dagger_nearby_attack_after_continuation}
  };

  #alias {dagger_nearby_attack_after_continuation} {
    dagger_mode {$dagger_nearby_target}
      {trampoline {dagger_nearby_attack} {dagger_nearby_farlook}}
      {dagger_nearby_attack_fail}
  };

  #alias {dagger_nearby_attack_fail} {
    debug DAGGER-NEARBY-FAIL: {%%1};
    #switch {"%%1"} {
      #case {"fighting"} {dagger_nearby_fighting};
      #case {"no-dagger"} {dagger_nearby_fail {no-dagger}};
      #case {"no-mob"} {dagger_nearby_done};
      #default {dagger_nearby_done}
    }   
  };

  #alias {dagger_nearby_fighting} {
    debug DAGGER-NEARBY: fighting --> nop;
    fish_end
  };

  hook {on_rip} {add} {dagger_nearby} {
    trampoline {dagger_nearby} {debug TRAMPOLINE --> farlook; dagger_nearby_farlook};
    %%1
  };

  #class {dagger_nearby} {save};
  #class {dagger_nearby} {close};

  dagger_nearby_farlook
}

#class {dagger} {save}
#class {dagger} {close}