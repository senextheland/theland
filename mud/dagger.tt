#class {dagger} {open}

#alias {dagger_mode} {fish_once}
#alias {dagger_mode_end} {fish_once_end}

#alias {dagger_all_end} {#nop}
#alias {dagger_all} {
  #class {dagger_all} {open};

  #list {dagger_all_targets} {create} {%1};  
  #alias {dagger_all_done} {dagger_all_end; %2};
  #alias {dagger_all_fail} {dagger_all_end; %3};
  
  #var {dagger_all_current_target} {};
  #alias {dagger_all_end} {
    hook {on_rip} {remove} {dagger_all};
    dagger_mode_end;
    #class {dagger_all} {kill}
  };

  #alias {dagger_all_look} {
    exec {dagger_all_look} {cmd_farlook $dagger_all_current_target {}}
      {dagger_all_attack}
      dagger_all_farlook_fail  
  };

  #alias {dagger_all_attack} {
    zone_continuation {dagger_all_attack_after_continuation}
  };

  #alias {dagger_all_attack_after_continuation} {
    #var {dagger_all_current_target};
    dagger_mode {$dagger_all_current_target}
      {trampoline {dagger_all_attack} {dagger_all_attack}}
      {dagger_all_attack_fail}
  };

  #alias {dagger_all_attack_fail} {
    debug DAGGER-ALL-FAIL: {%%1};
    #switch {"%%1"} {
      #case {"fighting"} {dagger_all_fighting};
      #case {"no-target"} {dagger_all_fighting};      
      #case {"no-dagger"} {dagger_all_fail {no-dagger}};
      #case {"no-mob"} {dagger_all_next};
      #default {dagger_all_next}
    }   
  };

  #alias {dagger_all_farlook_fail} {
    debug DAGGER-ALL-FARLOOK-FAIL --> %%1;
    #switch {"%%1"} {
      #case {"here"} {dagger_all_kill_room};
      #case {"not-found"} {dagger_all_next};
      #case {"fighting"} {dagger_all_kill_room};
      #default {debug dagger_all_farlook_fail UNRECOGNIZED COMMAND {%%1}}
    }    
  };

  #alias {dagger_all_fighting} {
    debug DAGGER-ALL: fighting --> nop;
    dagger_mode_end    
  };

  #alias {dagger_all_kill_room} {
    kill_room {$dagger_all_current_target}
      {dagger_all_look}
  };

  hook {on_rip} {add} {dagger_all} {
    trampoline {dagger_all} {debug TRAMPOLINE --> attack; dagger_all_attack};
    %%1
  };

  #alias {dagger_all_next} {    
    #list {dagger_all_targets} {get} {1} {dagger_all_current_target};
    #if { "$dagger_all_current_target" == "0" } {
      dagger_all_done
    } {
      #list {dagger_all_targets} {delete} {1};
      dagger_all_look
    }
  };

  #class {dagger_all} {save};
  #class {dagger_all} {close};

  dagger_all_next
}

#alias {dagger_walk_end} {#nop}
#alias {dagger_walk_path} {
  #class {dagger_walk_path} {open};

  #var {dagger_walk_path} {%1};
  #var {dagger_walk_target} {%2};
  #if {"%3" == "Immediately" || "%3" == "Nearby"} {
    #var {dagger_walk_range} {%3};
    #alias {dagger_walk_done} {dagger_walk_end; %4};
    #alias {dagger_walk_fail} {dagger_walk_end; %5}  
  } {
    #var {dagger_walk_range} {};  
    #alias {dagger_walk_done} {dagger_walk_end; %3};
    #alias {dagger_walk_fail} {dagger_walk_end; %4}
  };

  #alias {dagger_walk_kill_room} {
    kill_room {$dagger_walk_target}
      {dagger_walk_kill_room_done}
  };

  #alias {dagger_walk_kill_room_done} {
    #if {"$dagger_walk_range" == ""} {
      debug DAGGER-WALK-KILL-ROOM-DONE --> dagger-all;
      dagger_all {$dagger_walk_target}
        {dagger_walk_throw_done}
        {dagger_walk_throw_fail %%0}
    } {
      debug DAGGER-WALK-KILL-ROOM-DONE --> dagger_nearby $dagger_walk_range;
      dagger_nearby {$dagger_walk_target} {} {$dagger_walk_range}
        {dagger_walk_throw_done}
	dagger_walk_throw_fail
    }
  };

  #alias {dagger_walk_throw_done} {
    debug DAGGER-WALK-THROW-DONE --> step;
    step
  };

  #alias {dagger_walk_throw_fail} {
    debug DAGGER-WALK-THROW-FAIL: %%0;
    #switch {"%%1"} {
      #case {"no-mob"} {dagger_walk_throw_done};
      #default {dagger_walk_fail %%0}
    }
  };

  #class {dagger_walk_path} {save};
  #class {dagger_walk_path} {close};

  #alias {dagger_walk_end} {
    #class {dagger_walk_path} {kill};
    #alias {dagger_walk_end} {#nop}
  };

  walk_each {%1} {dagger_walk_kill_room} {dagger_walk_done} {dagger_walk_fail};
  dagger_walk_kill_room
}

#alias {dagger_nearby} {
  #class {dagger_nearby} {open};

  #var {dagger_nearby_target} {%1};
  #var {dagger_nearby_direction} {%2};
  #var {dagger_nearby_range} {%3};
  #alias {dagger_nearby_done} {dagger_nearby_end; %4};
  #alias {dagger_nearby_fail} {dagger_nearby_end; %5};

  #alias {dagger_nearby_end} {
    hook {on_rip} {remove} {dagger_nearby};
    #class {dagger_nearby} {kill}    
  };

  #alias {dagger_nearby_scan} {
    debug DAGGER-NEARBY-SCAN: {$dagger_nearby_direction} {$dagger_nearby_range};
    exec {dagger_nearby_scan} {cmd_scan_dir {$dagger_nearby_direction} {$dagger_nearby_range}}
      {dagger_nearby_scan_found}
      dagger_nearby_scan_fail  
  };

  #alias {dagger_nearby_scan_found} {
    debug DAGGER-NEARBY-SCAN-FOUND;
    dagger_nearby_attack
  };

  #alias {dagger_nearby_scan_fail} {
    debug DAGGER-NEARBY-SCAN-FAIL --> %%1;
    #switch {"%%1"} {
      #case {"not-found"} {dagger_nearby_done};
      #case {"fighting"} {dagger_nearby_kill_room};
      #default {debug dagger_nearby_scan_fail UNRECOGNIZED COMMAND {%%1}}
    }  
  };

  #alias {dagger_nearby_kill_room} {
    kill_room {$dagger_nearby_target}
      {dagger_nearby_scan}
  };

  #alias {dagger_nearby_throw_done} {
    debug DAGGER-NEARBY-THROW-DONE --> step;
    dagger_nearby_done
  };

  #alias {dagger_nearby_throw_fail} {
    debug DAGGER-NEARBY-THROW-FAIL: %%0;
    #switch {"%%1"} {
      #case {"no-mob"} {dagger_nearby_throw_done};
      #default {dagger_nearby_fail %%0}
    }
  };

  #alias {dagger_nearby_attack} {
    zone_continuation {dagger_nearby_attack_after_continuation}
  };

  #alias {dagger_nearby_attack_after_continuation} {
    dagger_mode {$dagger_nearby_target}
      {trampoline {dagger_nearby_attack} {dagger_nearby_scan}}
      {dagger_nearby_attack_fail}
  };

  #alias {dagger_nearby_attack_fail} {
    debug DAGGER-NEARBY-FAIL: {%%1};
    #switch {"%%1"} {
      #case {"fighting"} {dagger_nearby_fighting};
      #case {"no-target"} {dagger_nearby_fighting};
      #case {"no-dagger"} {dagger_nearby_fail {no-dagger}};
      #case {"no-mob"} {dagger_nearby_done};
      #default {dagger_nearby_done}
    }   
  };

  #alias {dagger_nearby_fighting} {
    debug DAGGER-NEARBY: fighting --> nop;
    dagger_mode_end
  };

  hook {on_rip} {add} {dagger_nearby} {
    trampoline {dagger_nearby} {debug TRAMPOLINE --> scan; dagger_nearby_scan};
    %%1
  };

  #class {dagger_nearby} {save};
  #class {dagger_nearby} {close};

  dagger_nearby_scan
}

#alias {collect_lost_fish} {
  cmd_check_container {$fish_container} {$expect_fish}
    {%3}
    {kill_walk_path {%1} {%2} {%3} {%4}}
}

#alias {dagger_walk_recollect} {
  dagger_walk_path {%1} {%2}
    {collect_lost_fish {%1} {%2} {%3} {%4}}
    {%4}
}

#class {dagger} {save}
#class {dagger} {close}