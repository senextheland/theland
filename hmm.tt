#class {hmm} {open}

#nop --------------------------------
#nop Utilities
#nop --------------------------------

#alias {debug} {
  #line ignore #show %0
}


#nop --------------------------------
#nop State
#nop --------------------------------

#var {my_state} {raw}

#alias {do_something} {
  #switch {"$my_state"} {
    #case {"raw"} {transition_state {raw} {kill}};
    #case {"kill"} {kill_target};
    #case {"walk"} {take_a_step};
    #case {"cooked"} {debug zone cooked! what we do now, fam?}
    #default {debug DO-SOMETHING unrecognized state=$my_state}
  }
}

#alias {force_state} {
  #local {to_state} {%1};
  #var {my_state} {$to_state}
}

#alias {transition_state} {
  #local {from_state} {%1};
  #local {to_state} {%2};

  #if {"$my_state" == "$from_state"} {
    #show CHANGE-STATE: $to_state
  } {
    debug TRANSITION-STATE: failed $from_state --> $to_state (state=$my_state)
  }
}

#nop Changing state happens through an action to avoid blowing up the call stack.
#action {CHANGE-STATE: %1} {
  #var {my_state} {%1};
  do_something
}


#nop --------------------------------
#nop Walking around
#nop --------------------------------

#var {target} {bryad}
#path {unzip} {1n1e1n2e1s1w3e2w1n2w1s2w1s1w2n2w2n2s2e2s1e1n1e1s}

#alias {take_a_step} {
  #path {walk}; #path {show}
}

#action {Exits:} {event_entered_room}
#alias {event_entered_room} {
  transition_state {walk} {kill}
}

#event {END OF PATH} {event_end_path}
#alias {event_end_path} {
  transition_state {walk} {cooked}
}


#nop --------------------------------
#nop Killing stuff
#nop --------------------------------

#alias {kill_target} {
  kill $target
}

#action {is dead! R.I.P.} {event_rip}
#alias {event_rip} {
  debug FIRE-EVENT-RIP!;
  transition_state {kill} {kill}
}

#action {They aren't here.} {event_not_here}
#alias {event_not_here} {
  debug FIRE-EVENT-NOT-HERE;
  transition_state {kill} {walk}
}

#action {No way! You are fighting for your life!} {event_fighting}
#alias {event_fighting} {  
  #if {"$my_state" == "walk"} {
    debug FIRE-EVENT-FIGHTING: tried to walk --> revert step;
    #path {move} {-1};
    force_state {kill}
  } {
    debug FIRE-EVENT-FIGHTING state=$my_stte --> no action
  }
}

#class {hmm} {save}
#class {hmm} {close}