#var {me} {senex}
#session $me theland.notroot.com 5000
#split 0 0
#config {speedwalk} {on}
#log {append} {$me.log}

#ticker {fidget} {emote glances around.} {120}

#alias {get_ready} {
  #read {mud/logging.tt};
  #read {mud/task.tt};
  #read {mud/exec.tt};
  #read {mud/hook.tt};
  #read {mud/commands.tt};
  #read {mud/tick.tt};
  #read {mud/cast.tt};
  #read {mud/eq-repair.tt};
  #read {mud/robot.tt};
  #read {mud/fight.tt};
  #read {mud/bash.tt};
  #read {mud/protect.tt};
  #read {mud/assist.tt};
  #read {mud/walk.tt};
  #read {mud/pawn.tt};
  #read {mud/regen.tt};
  #read {mud/recover.tt};
  #read {mud/kill.tt};
  #read {mud/fish.tt};
  #read {mud/dagger.tt}; 
  #read {mud/leader.tt};
  #read {mud/control.tt};
  #read {mud/zones.tt};
  #read {mud/spellup.tt};
  #read {mud/scary-things.tt};

  cmd_score {
    #read {mud/prompt.tt};
    #read {mud/health.tt};

    #var {health_low} {450}
  };

  hook {bless_down} {add} {$me} {cmd_cast_me {bless} {debug BLESS UP} {debug BLESS FAIL}};
  hook {armor_down} {add} {$me} {cmd_cast_me {armor} {debug ARMOR UP} {debug ARMOR FAIL}};
  need_spell_add {spiritual armor};
  hook {on_eq_repair} {add} {bless} {bless_eq_item};
  hook {on_eq_bless_unrepairable} {add} {bless} {bless_all_unrepairable};

  set_robot {silk} {Silk};
  protect {$robot_description} {$robot};
  reset_weapon_state
}

#alias {reload} {
  #read {mud/adapter.tt};
  #read {senex-test.tt};
  get_ready
}

#alias {bless_eq_item} {
  cmd_cast {cast 'bless' $eq_item_name}
    {on_eq_blessed; %1}
    {debug BLESS-EQ-ITEM: FAIL --> continue anyway; %1}
}

#alias {bless_all_unrepairable} {
  #if {"$eq_needs_bless[The Holy Grail]" == "true"} {
    #var {eq_item_name} {holy-grail};
    #var {eq_item_title} {The Holy Grail};
    rem holy-grail;
    bless_eq_item
      {grab holy-grail; %1}
      {grab holy-grail; %2}
  } {
    %1
  }
}

#alias {load_begin_loop} {
  zone_loop
    {zone_tridac_kill}  
    {zone_despair_se_walk}    
    {zone_bryad_walk}
    {zone_tridac_kill}
    {zone_x2_kill_with_darkwiz}
    {zone_tridac_kill}    
    {zone_tzen}
    {zone_tzen_walk_sell_magicite}
}

#alias {begin_loop} {
  load_begin_loop;
  zone_loop_start
}

#alias {load_overnight_loop} {
  zone_loop
    {zone_tridac_kill}  
    {zone_despair_se_walk}
    {zone_despair_walk_sell_basalt}
    {zone_antipal_kill}    
    {zone_x2_kill}
    {zone_tridac_kill}    
    {zone_despair_sw_walk}
    {zone_tzen_all}
    {zone_tzen_walk_sell_magicite}
    {zone_bryad_walk}    
}

#alias {overnight_loop} {
  load_overnight_loop;
  zone_loop_start
}

#alias {load_gim_loop} {
  protect Gim;
  zone_loop
    {zone_tridac_kill}  
    {zone_despair_maze}
    {zone_despair_walk_sell_basalt}    
    {zone_antipal_kill}    
    {zone_x2_kill_through_darkwiz_quick}
    {zone_tridac_kill}
    {zone_tzen_all}
    {zone_tzen_walk_sell_magicite}
    {zone_bryad_walk}
}

#alias {overnight_loop} {
  load_overnight_loop;
  zone_loop_start
}

#alias {load_test_loop} {
  zone_loop
    {zone_despair_sw_walk}
    {zone_despair_se_walk}
    {zone_x2_kill_with_darkwiz}
    {zone_tzen_all}
    {zone_tzen_walk_sell_magicite}
    {zone_bryad_walk}        
}

#alias {test_loop} {
  load_test_loop;
  zone_loop_start
}

#alias {load_keys_loop} {
  zone_loop
    {zone_despair_sw_walk}
    {zone_x2_kill_with_castle}
    {zone_darkwiz_castle}
    {zone_tzen_all}
    {zone_tzen_walk_sell_magicite}  
}

#alias {keys_loop} {
  load_keys_loop;
  zone_loop_start
}

#nop --------------------------
#nop adapter.tt
#nop --------------------------

#alias {cmd_heal_me} {
  cmd_layhands {$me} {%1} {%2}
}

#var regen_bag {xanth-bag}
#alias {regen} {
  rest;
  get stars-cloak $regen_bag; rem mane; wear stars-cloak neck;
  get purple-cape $regen_bag; rem lionskin-suit; wear purple-cape;
  get mithril-helmet $regen_bag; rem tuor-helm; wear mithril-helmet;
  get all.locust-skin $regen_bag; rem trousers; wear skin legs;
  get sapphire-ring $regen_bag; rem holiday-ring; wear sapphire-ring
}
 
#alias {unregen} {
  stand;
  rem sapphire-ring; wear holiday-ring; safe_put sapphire-ring $regen_bag;
  rem mithril-helmet; wear tuor-helm; safe_put mithril-helmet $regen_bag;
  rem stars-cloak; safe_put stars-cloak $regen_bag; wear mane;  
  rem purple-cape; wear lionskin-suit; safe_put purple-cape $regen_bag;
  rem locust-skin; safe_put all.locust-skin $regen_bag; wear trousers
}

#nop Heavy Tower Shield is IMM FIRE
#var {imm} {{fire}{on} {energy}{on} {poison}{off} {water}{on}}

#alias {_imm_energy_on} {
  get spectral-band xanth-bag;
  rem broons-bane; wear spectral-band;
  safe_put broons-bane xanth-bag
}

#alias {_imm_energy_off} {
  get broons-bane xanth-bag;
  rem spectral-band; wear broons-bane;
  safe_put spectral-band xanth-bag
}

#alias {imm_poison_on} {
  get grasshopper-skin xanth-bag;
  rem mane; wear skin neck;
  safe_put mane xanth-bag
}

#alias {imm_poison_off} {
  get mane xanth-bag;
  rem grasshopper-skin; wear mane;
  safe_put grasshopper-skin xanth-bag
}

#alias {imm_electric_on} {
  get bloom xanth-bag;
  rem shard; grab bloom;
  safe_put shard xanth-bag
}

#alias {imm_electric_off} {
  get shard xanth-bag;
  rem bloom; grab shard;
  safe_put bloom xanth-bag
}

#alias {imm_transition} {
  #if {"$imm[%1]" != "%2"} {
    #var {imm[%1]} {%2};
    imm_%1_%2
  } {
    #show IMM ALREADY %1 --> %2    
  }
}

#alias {cmd_imm_fire_on} {%1}
#alias {cmd_imm_fire_off} {%1}
#alias {cmd_imm_cold_on} {%1}
#alias {cmd_imm_cold_off} {%1}
#alias {cmd_imm_water_on} {%1}
#alias {cmd_imm_water_off} {%1}
#alias {cmd_imm_energy_on} {%1}
#alias {cmd_imm_energy_off} {%1}

#alias {_cmd_imm_energy_on} {
  cmd_imm_transition {energy} {on} {You wear Spectral Band} {%1} {%2}
}

#alias {_cmd_imm_energy_off} {
  cmd_imm_transition {energy} {off} {You wear broons bane} {%1} {%2}
}

#alias {cmd_imm_poison_on} {
  cmd_imm_transition {poison} {on} {You wear A Grasshopper's Skin} {%1} {%2}
}

#alias {cmd_imm_poison_off} {
  cmd_imm_transition {poison} {off} {You wear the lions mane} {%1} {%2}
}

#alias {cmd_imm_electric_on} {
  cmd_imm_transition {electric} {on} {You grab a Desert Bloom.} {%1} {%2}
}

#alias {cmd_imm_electric_off} {
  cmd_imm_transition {electric} {off} {You grab A Purple Shard.} {%1} {%2}
}

#var {nodrop_ring} {wind-ring}
#var {weapon_nodrop} {wind-sword}
#var {weapon_noscrap} {achilles-spear}
#var {weapon_align_evil} {fury}
#var {weapon_align_good} {fury}
#var {weapon_life_protect} {claw}
#var {current_weapon} {}

#var {mode_align} {}
#var {mode_scrap} {}
#var {mode_life_protect} {}

#alias {safe_put} {
  #if {"%1" != "$weapon_nodrop" && "%1" != "$nodrop_ring"} {
    put %1 %2
  } {
    debug I WILL NOT PUT NODROP ITEM {%1} IN %2
  }
}

#alias {reset_weapon_state} {
  #var {mode_align} {evil};
  #var {mode_scrap} {on};
  #var {mode_life_protect} {off};
  #var {current_weapon} {$weapon_align_evil};

  get $weapon_align_evil case; 
  rem $weapon_noscrap; safe_put $weapon_noscrap case;
  rem $weapon_align_good; safe_put $weapon_noscrap case;
  rem $weapon_life_protect; safe_put $weapon_life_protect case;
  
  wield $current_weapon
}

#alias {cmd_reset_weapon_state} {
  exec_task {cmd_reset_weapon_state} {reset_weapon_state} {%1} {%2} {
    #action {You wield the sword of the wind.} {task_cmd_reset_weapon_state_done};
    #action {You wield Clangeddin's Fury.} {task_cmd_reset_weapon_state_done};
    #action {You wield a massive turkey leg.} {task_cmd_reset_weapon_state_done};
    #action {You do not seem to have the} {task_cmd_reset_weapon_state_fail};
    #action {You are already wielding something.} {task_cmd_reset_weapon_state_fail}
  }
}

#alias {choose_best_weapon} {
  #local {last_weapon} {$current_weapon};

  #if {"$mode_scrap" == "off"} {
    #var {current_weapon} {$weapon_noscrap}
  }; #elseif {"$mode_life_protect" == "on"} {
    #var {current_weapon} {$weapon_life_protect}
  }; #elseif {"$mode_align" == "evil"} {
    #var {current_weapon} {$weapon_align_evil}
  }; #else {
    #var {current_weapon} {$weapon_align_good}
  };
  
  #if {"$last_weapon" != "$current_weapon"} {
    get $current_weapon case;  
    rem $last_weapon;
    wield $current_weapon;  
    safe_put $last_weapon case
  } {
    #show ALREADY WIELDING
  }
}

#alias {cmd_scrap_off} {
  #var {mode_scrap} {off};
  exec_task {cmd_scrap_off} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_scrap_off_done};
    #action {You wield Achilles' Spear.} {task_cmd_scrap_off_done};
    #action {You do not seem to have the} {task_cmd_scrap_off_fail};    
    #action {You are already wielding something.} {task_cmd_scrap_off_fail}
  }
}

#alias {cmd_scrap_on} {
  #var {mode_scrap} {on};
  exec_task {cmd_scrap_on} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_scrap_on_done};  
    #action {You wield The Holy Avenger.} {task_cmd_scrap_on_done};
    #action {You wield A glowing trident.} {task_cmd_scrap_on_done};
    #action {You wield Clangeddin's Fury.} {task_cmd_scrap_on_done};
    #action {You wield the sword of the wind.} {task_cmd_scrap_on_done};
    #action {You wield a massive turkey leg.} {task_cmd_scrap_on_done}; 
    #action {You do not seem to have the} {task_cmd_scrap_on_fail};    
    #action {You are already wielding something.} {task_cmd_scrap_on_fail}
  }
}

#alias {cmd_life_protect_on} {
  #var {mode_life_protect} {on};
  exec_task {cmd_life_protect_on} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_life_protect_on_done};  
    #action {You wield Claw of The Toucan.} {task_cmd_life_protect_on_done};
    #action {You do not seem to have the} {task_cmd_life_protect_on_fail};    
    #action {You are already wielding something.} {task_cmd_life_protect_on_fail}
  }
};

#alias {cmd_life_protect_off} {
  #var {mode_life_protect} {off};
  exec_task {cmd_life_protect_off} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_life_protect_off_done};  
    #action {You wield} {task_cmd_life_protect_off_done};
    #action {You do not seem to have the} {task_cmd_life_protect_off_fail};    
    #action {You are already wielding something.} {task_cmd_life_protect_off_fail}
  }
}

#alias {cmd_mob_align_evil} {
  #var {mode_align} {evil};
  exec_task {cmd_mob_align_evil} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_mob_align_evil_done};
    #action {You wield the sword of the wind.} {task_cmd_mob_align_evil_done};
    #action {You wield The Holy Avenger.} {task_cmd_mob_align_evil_done};
    #action {You wield Achilles' Spear.} {task_cmd_mob_align_evil_done};;
    #action {You wield Clangeddin's Fury.} {task_cmd_mob_align_evil_done};
    #action {You wield a massive turkey leg.} {task_cmd_mob_align_evil_done};
    #action {You do not seem to have the} {task_cmd_mob_align_evil_fail};    
    #action {You are already wielding something.} {task_cmd_mob_align_evil_fail}
  }
}

#alias {cmd_mob_align_good} {
  #var {mode_align} {good};
  exec_task {cmd_mob_align_good} {choose_best_weapon} {%1} {%2} {
    #action {ALREADY WIELDING} {task_cmd_mob_align_good_done};
    #action {You wield the sword of the wind.} {task_cmd_mob_align_good_done};    
    #action {You wield The Holy Avenger.} {task_cmd_mob_align_good_done};
    #action {You wield Achilles' Spear.} {task_cmd_mob_align_good_done};
    #action {You wield Clangeddin's Fury.} {task_cmd_mob_align_good_done};
    #action {You wield a massive turkey leg.} {task_cmd_mob_align_good_done};    
    #action {You do not seem to have the} {task_cmd_mob_align_good_fail};
    #action {You are already wielding something.} {task_cmd_mob_align_good_fail}
  }  
}

#alias {cmd_pick_lock} {
  cmd_issue_order {pick %1} {%2} {%3}
}

#alias {cmd_autoloot_junk} {
  debug AUTOLOOT-JUNK --> drop all.$autoloot_junk;
  trampoline {autoloot_junk} {drop all.$autoloot_junk};
  %1
}

#alias {autoloot_junk} {
  #if {"%1" != ""} {
    #var {autoloot_junk} {%1};
    hook {on_rip} {add} {autoloot_junk} {cmd_autoloot_junk}
  } {
    hook {on_rip} {remove} {autoloot_junk}    
  }
}

#function {autoloot_type} {
  autoloot_junk;
  #switch {"%1"} {
    #case {"no-cursed"} {#var {result} {on}};
    #case {"some-cursed"} {#var {result} {on}};
    #case {"heavy"} {
      #if {"$target" == "darkwiz" || "$target" == "undersea"} {
        autoloot_junk {$target};
        #var {result} {on}
      } {
        #var {result} {gold}
      }
    };    
    #default {#var {result} {%1}}
  }
}


#nop --------------------------
#nop Overrides
#nop --------------------------

#alias {sneak} {
  get pelt case; rem waistband; wear pelt; safe_put waistband case
}

#alias {unsneak} {
  rem pelt; safe_put pelt case; get waistband case; wear waistband
}

#alias {cmd_sneak} {
  exec_task {cmd_sneak} {sneak} {%1} {%2} {
    #action {You wear The Pelt of the Golden Fox about your waist.} {task_cmd_sneak_done};
    #action {You are not using it.} {task_cmd_sneak_done}
  }
}

#alias {cmd_unsneak} {
  exec_task {cmd_unsneak} {unsneak} {%1} {%2} {
    #action {You wear Mystic Ruby Waistband about your waist.} {task_cmd_unsneak_done};
    #action {You are not using it.} {task_cmd_unsneak_done}
  }
}


#nop --------------------------
#nop Follow orders
#nop --------------------------

#class {follow_orders} {open}

#alias {cmd_spirit_armor_down} {
  debug ORDER: marking spiritual armor --> down;
  spell_state_down {spiritual armor};
  quick_spellup_continuation {%1}
}

#list {allow_orders} {create}
  {Sid}
  {Silk}
  {group}

#alias {do_order} {
  #local {allow} {0};
  #list {allow_orders} {find} {%1} {allow};
  debug DO ORDER {%1} {%2} allow=$allow;
  
  #if {"$allow" == "0"} {
    order_tell {%1} {FAIL: %2 -- not allowed}
  } {
    #switch {"%2"} {
      #case {"cast"} {
        exec_cast {%3} {%4}
	  {order_done {%1} {cast %3}}
	  {order_fail {%1} {cast %3}}
      };
      #case {"imm"} {order_dispatch {%2} {cmd_imm_%3_%4} {%1} {%2}};
      
      #case {"layhands"} {order_dispatch {%2} {cmd_layhands %3} {%1} {%2}};
      #case {"recall"} {executor {global_tasks} {clear}; order_dispatch {%2} {cmd_recall} {%1} {%2}};
      #case {"noscrap"} {order_dispatch {%2} {cmd_scrap_off} {%1} {%2}};
      #case {"scrap"} {order_dispatch {%2} {cmd_scrap_on} {%1} {%2}};
      #case {"scrappy"} {order_dispatch {%2} {cmd_scrap_on} {%1} {%2}};
      #case {"stand"} {order_dispatch {%2} {cmd_stand} {%1} {%2}};
      #case {"spiritual armor"} {order_dispatch {spirit_armor} {cmd_spirit_armor_down} {%1} {%2}};
	
      #default {order_tell {%1} {FAIL: %2 -- unrecognized order}}
    }
  }
}
	
#alias {order_dispatch} {
  debug ORDER-DISPATCH %1 %2 %3;
  exec {order_%1} {%2} {order_done {%3} {%4}} {order_fail {%3} {%4}}
}

#alias {order_tell} {
  #if {"%1" == "group"} {
    tell group '%2
  } {
    tell %1 %2
  }  
}

#alias {order_done} {
  order_tell {%1} {OK: %2}
}

#alias {order_fail} {
  #if {"%3" == ""} {
    order_tell {%1} {FAIL: %2}
  } {
    order_tell {%1} {FAIL: %2 -- %3}
  }
}

#action {%1 tells the group 'cast {armor|bless|cure blind}'} {do_order {group} {cast} {%2} {%1}}
#action {%1 tells the group 'imm: {fire|energy|poison|electric|water} {on|off}'} {do_order {group} {imm} {%2} {%3}}
#action {%1 tells you 'cast {armor|bless}'} {do_order {%1} {cast} {%2} {%1}}
#action {%1 tells you '{healme|lay|layhands}'} {do_order {%1} {layhands} {%1}}
#action {%1 tells the group '{healme|lay|layhands}'} {do_order {group} {layhands} {%1}}
#action {%1 tells the group '{recall|noscrap|scrap|scrappy}'} {do_order {group} {%2}}
#action {%1 whispers to you, 'down: %2'} {do_order {%1} {%2}}
 
#class {follow_orders} {save}
#class {follow_orders} {close}
